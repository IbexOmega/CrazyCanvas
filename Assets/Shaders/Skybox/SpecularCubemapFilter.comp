#version 460
#extension GL_GOOGLE_include_directive : enable

#include "CubemapHelper.glsl"

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

// Pushconstants
layout(push_constant) uniform PushConstants
{
	uint Size;
} u_PC;

// Textures
layout(binding = 0, set = 0) uniform samplerCube u_Source;
layout(binding = 1, set = 0, rgba16f) restrict writeonly uniform imageCube	u_CubeMap;

void main()
{
	vec2 texCoord	= vec2(gl_GlobalInvocationID.xy);
	if (texCoord.x > u_PC.Size || texCoord.y > u_PC.Size)
	{
		return;
	}

	int face = int(gl_GlobalInvocationID.z);

	// Map the UV coords of the cubemap face to a direction
	// [(0, 0), (1, 1)] => [(-0.5, -0.5), (0.5, 0.5)]
	vec3 direction	= normalize(vec3((texCoord.xy / float(u_PC.Size)) - 0.5f, 0.5f));
	direction		= normalize(ROTATE_UV[face] * direction);
	
	vec4 color = texture(u_Source, direction);
	imageStore(u_CubeMap, ivec3(texCoord, face), color);
}